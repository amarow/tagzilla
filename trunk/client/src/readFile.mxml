<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute">

	<mx:Script>
		<![CDATA[
			import flash.net.FileReference;
			import flash.net.FileFilter;

			import flash.events.IOErrorEvent;
			import flash.events.Event;

			import flash.utils.ByteArray;

			//FileReference Class well will use to load data
			private var fr:FileReference;

			//File types which we want the user to open
			private static const FILE_TYPES:Array = [new FileFilter("Text File", "*.txt;*.text")];

			//called when the user clicks the load file button
			private function onLoadFileClick():void
			{
				//create the FileReference instance
				fr = new FileReference();

				//listen for when they select a file
				fr.addEventListener(Event.SELECT, onFileSelect);

				//listen for when then cancel out of the browse dialog
				fr.addEventListener(Event.CANCEL,onCancel);

				//open a native browse dialog that filters for text files
				fr.browse(FILE_TYPES);
			}

			/************ Browse Event Handlers **************/

			//called when the user selects a file from the browse dialog
			private function onFileSelect(e:Event):void
			{
				//listen for when the file has loaded
				fr.addEventListener(Event.COMPLETE, onLoadComplete);

				//listen for any errors reading the file
				fr.addEventListener(IOErrorEvent.IO_ERROR, onLoadError);

				//load the content of the file
				fr.load();
			}

			//called when the user cancels out of the browser dialog
			private function onCancel(e:Event):void
			{
				trace("File Browse Canceled");
				fr = null;
			}

			/************ Select Event Handlers **************/

			//called when the file has completed loading
			private function onLoadComplete(e:Event):void
			{
				//get the data from the file as a ByteArray
				var data:ByteArray = fr.data;

				//read the bytes of the file as a string and put it in the
				//textarea
				outputField.text = data.readUTFBytes(data.bytesAvailable);

				//clean up the FileReference instance

				fr = null;
			}

			//called if an error occurs while loading the file contents
			private function onLoadError(e:IOErrorEvent):void
			{
				trace("Error loading file : " + e.text);
			}

		]]>
	</mx:Script>

	<mx:Button label="Load Text File" right="10" bottom="10" click="onLoadFileClick()"/>
	<mx:TextArea right="10" left="10" top="10" bottom="40" id="outputField"/>

</mx:Application>