<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute">

	<mx:Script>
		<![CDATA[

			import flash.net.FileReference;

			import flash.events.IOErrorEvent;
			import flash.events.Event;

			private static const DEFAULT_FILE_NAME:String = "example.txt";

			//FileReference Class well will use to save de.ama.tagzilla.data
			private var fr:FileReference;

			/********** UI Event Handlers **************/

			//called when the users types in the textarea
			//note valueCommit should be used, but is broken in the flex build 
			//I am using
			private function onInputChange():void
			{
				//enable button if there is any text to save
				saveButton.enabled = (inputField.text.length > 0);
			}

			//called when the user clicks the load file button
			private function onSaveClick():void
			{
				//create the FileReference instance
				fr = new FileReference();

				//listen for the file has been saved
				fr.addEventListener(Event.COMPLETE, onFileSave);

				//listen for when then cancel out of the save dialog
				fr.addEventListener(Event.CANCEL,onCancel);

				//listen for any errors that occur while writing the file
				fr.addEventListener(IOErrorEvent.IO_ERROR, onSaveError);

				//open a native save file dialog, using the default file name
				fr.save(inputField.text, DEFAULT_FILE_NAME);
			}

			/***** File Save Event Handlers ******/

			//called once the file has been saved
			private function onFileSave(e:Event):void
			{
				trace("File Saved");
				fr = null;
			}

			//called if the user cancels out of the file save dialog
			private function onCancel(e:Event):void
			{
				trace("File save select canceled.");
				fr = null;
			}

			//called if an error occurs while saving the file
			private function onSaveError(e:IOErrorEvent):void
			{
				trace("Error Saving File : " + e.text);
				fr = null;
			}
		]]>
	</mx:Script>

	<mx:Button label="Save File" right="10" bottom="10" id="saveButton"
											click="onSaveClick()" enabled="false"/>
	<mx:TextArea right="10" left="10" top="36" bottom="40" id="inputField"
									editable="true" change="onInputChange()"/>
	<mx:Label text="Enter text to save" left="10" top="10" fontWeight="bold"/>

</mx:Application>